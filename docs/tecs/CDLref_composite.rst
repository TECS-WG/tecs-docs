.. _CDLref-composite:

複合セルタイプ記述
===================

複合セルタイプは、1つ以上のセルを組合わせて新しいセルタイプを定義するものです。
複合セルタイプは、複数のセルを組合わせる目的以外に、属性の初期値を与えたり、固定結合させるのに用いることもできます。

【記述例】

.. code-block:: tecs-cdl

  [singleton]                             // 複合セルタイプの指定子
  composite tCompositeCelltype {          // composite キーワードと複合セルタイプ名
      entry sSignature eEntry;            // entry  キーワード、対応付けられたシグニチャ名、受け口名
      call  sSignature cCall;             // call  キーワード、対応付けられたシグニチャ名、呼び口名
      attr {                              // 属性
          int32_t val = 10;
      };

      // 内部セル
      cell tCelltype Cell1 {
          cCellCall = Cell2.eEntry;       // 内部セルへの結合
          val = composite.val;            // 属性のエクスポート
      };
      cell tCelltype Cell2 {
          cCall => composite.cCall;       // 呼び口のエクスポート
      };
      composite.eEntry => cell2.eEntry;   // 受け口のエクスポート
  };


複合セルタイプは、セルタイプと同様に、呼び口、受け口、属性、内部セルを持ちます。
複合セルタイプに組み入れられるセルを「内部セル」と呼びます。

複合セルタイプに属するセルの生成は、複合セルタイプの内部セルのコピーが作成されるとともに、呼び口の結合先や、属性の初期値が整えられます。

複合セルタイプの指定子
----------------------

アクティブ(active)指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

アクティブ(active)指定子は、少なくとも一つの内部セルのセルタイプがアクティブである場合、指定する必要があります。

シングルトン(singleton)指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

シングルトン(singleton)指定子は、少なくとも一つの内部セルのセルタイプがシングルトンである場合、指定する必要があります。

シングルトン(singleton)指定子は、内部セルのセルタイプにシングルトンのものがない場合でも指定できます。
この場合、複合セルタイプのセルは、1つだけ定義することができます。

受け口
------

複合セルタイプの受け口は、内部セルの呼び口を複合セルタイプの受け口としてエクスポートするものです。
受け口配列とすることもできます。

受け口の指定子
--------------

アロケータ(allocator)指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

アロケータ(allocator)指定子は、リレーアロケータまたは内部アロケータを指定するものです。

リレーアロケータの指定
::::::::::::::::::::::
  
【記述例】

.. code-block:: tecs-cdl

  composite tCompRelayAlloc {
      [allocator( func.a    <= cCallExt.func.a,
           func2.buf <= cCallExt.func2.buf )]
      entry sSig eEntExt;
      call sSig cCallExt;

      cell tRelay Cell1 {
          cCall => composite.cCallExt;
      };

      composite.eEntExt => Cell1.eEnt;
  };

T.B.W.

内部アロケータの指定
::::::::::::::::::::

【記述例】

.. code-block:: tecs-cdl

  composite tCompAlloc {
     entry sAlloc eAlloc;
     [allocator(
          func.a=eAlloc,
          func2.buf=eAlloc)]
     entry sSig  eEntExt;
     attr {
         int32_t  num;
     };

     [allocator(
         eEnt.func.a=Alloc.eA,
         eEnt.func2.buf=Alloc.eA)]
     cell tCt1 Cell1 {
         num = composite.num;
     };

     cell tAlloc Alloc{
         num = composite.num;
     };

     cell tAlloc Alloc2{
         num = composite.num;
     };

     composite.eEntExt => Cell1.eEnt;
     composite.eAlloc  => Alloc.eA;
  };

T.B.W.

使用しない指定子
````````````````

以下の指定子は、複合セルタイプの受け口では指定しません。

 * インライン(inline)指定子

インライン(inline)指定子は、セルタイプの実装において必要になるもので、セルタイプの利用者にとって、必要な情報ではありません。

固定結合
------------------

複合セルタイプにおいても、セルタイプの :ref:`cdlref-celltype-fixed` と同様に固定結合を指定できます。

内部セルのセルタイプにおいても固定結合が指定されている場合、複合セルタイプでしてされた固定結合と内部セルのセルタイプにおいて指定された固定結合の両方とも結合されます。

呼び口
------

複合セルタイプの呼び口は、内部セルの呼び口を複合セルタイプの受け口としてエクスポートするものです。
呼び口配列とすることもできます。

呼び口の指定子
--------------

オプショナル(optional)指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

オプショナル(optional)指定子は、内部セルのセルタイプにおいて optional と指定された受け口をエクスポートする場合に指定します。

属性
----

複合セルタイプの属性は、内部セルの属性をエクスポートするものです。

複合セルタイプの属性において初期値を与えることができます。
この場合、内部セルの属するセルタイプの属性において指定された初期値を上書きします。

C_EXP の名前置換
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

composite における名前置換は、特別な規則が適用されます。
composite の attr に現れる C_EXP における $id$, $ct$, $cell$ の名前置換では、複合セルタイプの名前、複合セルの名前に置換されます。
他の名前置換は、複合セルタイプの内部セルが展開されてコピーされたセルの名前によって置換されます。

以下に、複合セルタイプにおける名前置換の例を示します。
次のような TECS CDL 記述があるものとします。

【記述例】

.. code-block:: tecs-cdl

  composite tComposite {
    attr {
      int32_t  a = C_EXP( "A_$id$" );
    };
    cell tCelltype Cell1 {
      a = composite.a;
      b = C_EXP( "B_$id$" );
    };
    cell tCelltype2 Cell2 {
      a = composite.a;
      c = C_EXP( "C_$id$" );
   };
  };

  cell tComposite CompositeCell {
  };

CompositeCell における、名前置換の結果は A_$id$ は "A_CompositeCell" に、B_$id$ は "B_CompositeCell_Cell" に、C_$id$ は "C_Composite_Cell2" となります。
属性 a は Cell1, Cell2 ともに、同じ初期値を持つことになります。

【仕様決定の理由】この仕様は、タスクとタスク例外を別のセルとして生成し、それらを composite で一つのセルにまとめる際に、タスクの ID の名前を一致させるために導入された（他の用途でも使用してもよい）


属性の指定子
------------

size_is 指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

size_is 指定子は、エクスポートする属性の、元のセルタイプ定義において指定されている場合に指定します。

使用しない指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

以下の指定子は、複合セルタイプの属性では指定しません。

 * 省略(omit)指定子

省略(omit)指定子は、セルタイプの実装において必要になるもので、セルタイプの利用者にとって、必要な情報ではありません。

内部セル
--------

内部セルは、複合セルタイプを構成するセルです。
複合セルタイプは、1個以上の内部セルを持ちます。

呼び口のエクスポート
--------------------

内部セルの呼び口を複合セルタイプの呼び口としてエクスポートできます。
内部セル内に記述します。

エクスポートするものが、受け口配列の場合、配列全体をエクスポートする必要があります。
特定の添数のみエクスポートすることはできません。

受け口のエクスポート
--------------------

内部セルの受け口を複合セルタイプの受け口としてエクスポートできます。
内部セルの外側に記述します。

エクスポートするものが、受け口配列の場合、配列全体をエクスポートする必要があります。
特定の添数のみエクスポートすることはできません。

属性のエクスポート
------------------

内部セルの属性を複合セルタイプの属性としてエクスポートできます。
内部セル内に記述します。

内部セルの指定子
----------------

アロケータ(allocator)指定子
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

内部セルのアロケータ(allocator)指定子は、セル記述のアロケータ指定子と同様です。
この場合のアロケータは、内部セルです。

使用しない指定子
----------------

以下の指定子は、複合セルタイプでは指定する必要がありません。

 * 省略(omit)指定子
 * インライン(inline)指定子

これらの指定子は、セルタイプの実装において必要になるもので、セルタイプの利用者にとって、必要な情報ではありません。

